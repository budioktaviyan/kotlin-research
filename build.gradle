buildscript {
    ext {
        kotlinVersion = '1.0.5-2'
        vertxVersion = '3.3.3'
        logbackVersion = '1.1.7'
        kovenantVersion = '3.3.0'
        apacheTikaVersion = '1.13'
        commonsLangVersion = '3.4'
        daggerVersion = '2.7'
    }

    repositories {
        jcenter()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
    }
}

plugins {
    id 'application'
    id 'com.github.johnrengelman.shadow' version '1.2.4'
}

apply plugin: 'kotlin'
apply plugin: 'kotlin-kapt'

group 'id.jasoet.dagger'
version '1.0'

mainClassName = 'io.vertx.core.Launcher'
def mainVerticleName = 'io.vertx.example.MainVerticle'

repositories {
    jcenter()
}

dependencies {
    compile "io.vertx:vertx-core:$vertxVersion"
    compile "io.vertx:vertx-web:$vertxVersion"
    compile "io.vertx:vertx-mongo-client:$vertxVersion"

    compile "nl.komponents.kovenant:kovenant:$kovenantVersion"

    compile "com.google.dagger:dagger:$daggerVersion"
    kapt "com.google.dagger:dagger-compiler:$daggerVersion"

    compile "org.apache.commons:commons-lang3:${commonsLangVersion}"

    compile "ch.qos.logback:logback-classic:$logbackVersion"

    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
    compile "org.jetbrains.kotlin:kotlin-runtime:$kotlinVersion"
}

run {
    args = ['run', mainVerticleName,
            "--launcher-class=$mainClassName",
            "--redeploy=src/**/*.*",
            "--on-redeploy=./gradlew classes"
    ]
}

shadowJar {
    baseName = 'kotlin'
    classifier = 'vertx'

    manifest {
        attributes 'Main-Verticle': mainVerticleName
    }

    mergeServiceFiles {
        include 'META-INF/services/io.vertx.core.spi.VerticleFactory'
    }
}


task wrapper(type: Wrapper) {
    gradleVersion = '3.1'
}

